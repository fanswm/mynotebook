Content-Type: text/x-zim-wiki
Wiki-Format: zim 0.4
Creation-Date: 2017-11-27T12:51:30+08:00

====== 批量合并相同值单元格 ======
Created Monday 27 November 2017

http://mp.weixin.qq.com/s?__biz=MzUzODI3ODk1Mw==&mid=2247483920&idx=1&sn=b8af5eaf9fbae6f4219bf26a36530f31&chksm=fadb6f56cdace6404065a1a3248ae989ca0a48dd7ea498f03889649961b8f719d9c4c22d3271&mpshare=1&scene=1&srcid=1127JRmCN7UCC7HwehRPbmYq#rd

代码如下：

```
Sub MergeRange() 
'EH技术论坛：VBA编程学习与实践 看见星光 
    Dim Rng As Range 
    Dim i&, Col&, Fist, Last 

    Application.ScreenUpdating = False 
    '取消屏幕更新 
    Application.DisplayAlerts = False 
    '取消信息提醒。当有值单元格被合并时屏蔽提示信息 

    Set Rng = Application.InputBox("请选择单列数据列！", Type:=8) 
    '用户选择数据列 
    Set Rng = Intersect(Rng.Parent.UsedRange, Rng) 
    'intersect语句避免用户选择整列造成无谓运算 
    Col = Rng.Column
     'Rng所在列 
    Fist = Rng.Row 
    'Rng开始行，用户选择的区域并不是一定从第一行开始，因此需要此句判断 
    Last = Fist + Rng.Rows.Count - 1 
    'Rng结束行 

    Rng.Parent.Select 
    '激活Rng对象所在的工作表 

    For i = Last To Fist + 1 Step -1 
    '对Rng进行从后向前遍历 
        If Cells(i, Col) = Cells(i - 1, Col) Then 
            Cells(i - 1, Col).Resize(2, 1).Merge 
        End If 
    Next 

    Rng.VerticalAlignment = xlCenter 
    '格式上下居中 
    MsgBox "合并完成。" 

    Application.ScreenUpdating = True 
    Application.DisplayAlerts = True 
End Sub
```

小贴士：
 
1，该段代码只支持单列单元格区域，例如A10:A20，A:A整列等。
 
2，由于merge合并后的单元格区域只保留右上角首个单元格的值，因此该段代码遍历单元格区域的顺序是从后向前，而不是从前向后，后者是新人们常出错之处。
 
3，当然也可以采用从前向后的遍历顺序，先将Rng装入数组，对数组中的数据进行遍历判断是否等同，标记头尾行号，再批量合并单元格。该方法的效率远远高于遍历单元格两两合并的方法。
